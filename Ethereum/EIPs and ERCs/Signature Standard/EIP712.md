# EIP-712 Type structured data hashing and signing

*A procedure for hashing and signing of typed structured data as opposed to just byte strings.*

## Overview

EIP-712 provides a standard for signing typed structured data in Ethereum, making signatures more secure and human-readable in wallet interfaces.

**Domain Separator:**

```solidity
struct EIP712Domain {
    string name;// Contract/protocol name
    string version;// Contract/protocol version
    uint256 chainId;// Chain ID for replay protection
    address verifyingContract;// Contract verifying the signature
    bytes32 salt;// Optional unique identifier
}
```

**Type Hash Construction:**

```solidity
// Basic format
"TypeName(type1 field1,type2 field2,...)"

// Nested structs
"MainType(type1 field1,NestedType field2)NestedType(type1 field1,type2 field2)"

```

## Encoding Rules

1. Atomic Types

```solidity
// Basic types encode as-is
address => abi.encode(address)
uint256 => abi.encode(uint256)
bool => abi.encode(bool)
bytes32 => abi.encode(bytes32)
```

2. Dynamic Types

```solidity
// Strings
string => keccak256(bytes(stringValue))

// Bytes
bytes => keccak256(bytesValue)
```

3. Arrays

```solidity
// Fixed and dynamic arrays
type[] => keccak256(abi.encodePacked(
    encode(element1),
    encode(element2),
    ...
))
```

4. Structs

```solidity
// Encoding order:
1. typeHash
2. encoded field values in order
struct MyStruct {
    uint256 field1;
    string field2; // dynamic type
}
=> keccak256(abi.encode(
    TYPE_HASH,
    field1,
    keccak256(bytes(field2))
))

```

## Implementation Example

```solidity
// 1. Define struct
struct Mail {
    address from;
    address to;
    string contents;
}

// 2. Define type hash
bytes32 constant MAIL_TYPEHASH = keccak256(
    "Mail(address from,address to,string contents)"
);

// 3. Implement hashing
function hashMail(Mail memory mail) public pure returns (bytes32) {
    return keccak256(abi.encode(
        MAIL_TYPEHASH,
        mail.from,
        mail.to,
        keccak256(bytes(mail.contents))
    ));
}
```

## **Nested Structs Example**

```solidity
// 1. Define structs
struct Person {
    string name;
    address wallet;
}

struct Mail {
    Person from;
    Person to;
    string contents;
}

// 2. Define type hashes
bytes32 constant PERSON_TYPEHASH = keccak256(
    "Person(string name,address wallet)"
);

bytes32 constant MAIL_TYPEHASH = keccak256(
    "Mail(Person from,Person to,string contents)Person(string name,address wallet)"
);

// 3. Implement hashing
function hashPerson(Person memory person) public pure returns (bytes32) {
    return keccak256(abi.encode(
        PERSON_TYPEHASH,
        keccak256(bytes(person.name)),
        person.wallet
    ));
}

function hashMail(Mail memory mail) public pure returns (bytes32) {
    return keccak256(abi.encode(
        MAIL_TYPEHASH,
        hashPerson(mail.from),
        hashPerson(mail.to),
        keccak256(bytes(mail.contents))
    ));
}

```

## Final Encoding

```solidity
// Final encoding includes:
bytes32 digest = keccak256(abi.encodePacked(
    "\x19\x01",
    domainSeparator,
    hashStruct(message)
));
```